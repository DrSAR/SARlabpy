;****************************************************************
;
; Copyright (c) 2001 - 2003
; Bruker BioSpin MRI GmbH
; D-76275 Ettlingen, Germany
;
; All Rights Reserved
; 
; FLASH - a gradient echo imaging method
;
;****************************************************************
;
; d0 - TR padding
; d1 - T1 padding
; d2 - T2 padding
; d3 = PVM_RiseTime
; d4 = PVM_RampTime
; d8 = CONFIG_amplifier_enable

#include <MRI.include>
preset off

define delay denab
define delay d4d10u
define delay d5d20u
define delay d0d40u
define delay d4d2
define delay d3m5

"denab =  d3 - de"
"d4d10u = d4 +10u"
"d5d20u = d5 +20u"
"d0d40u = d0 -40u"
"d4d2 = d4 + d2"
"d3m5 = d3 - 5u"
"l3 = l1 + ds"

#include <PrepModulesHead.mod> 
subr FlowSatInit()

INIT_DEVICES

lgrad r2d<2d> = L[1]
zgrad r2d
lgrad r3d<3d> = L[2]
zgrad r3d


decr,   dgrad r2d
        lo to decr times DS



lgrad slice = NSLICES
zslice
        2.5u recph1 ph1
        2.5u recph2 ph1
        2.5u recph3 ph1
        2.5u recph4 ph1
start,  10u

if(PVM_BlBloodOnOff)
{
#include <TriggerPerPhaseStep.mod>
}
subr BlBlood(ph0)

#include <TriggerPerPhaseStep.mod>
subr SliceSelIr()

subr Tagging()
;----------------------------------start of the main loop ----------
slice, 	10u fq8b:f1 fq8b:f2 fq8b:f3 fq8b:f4

#include <TriggerPerSlice.mod>
;----------------------------------preparation modules -------------
subr FlowSat()
subr SatSlices()
subr MagTrans()
subr FatSup()

        d6	grad{(0)|(0)|(t9)}
;----------------------------------slice selection------------------	
	d3m5 	grad{(0)|(0)|(t0)} fq1:f1 fq1:f2 fq1:f3 fq1:f4
        5u reset:f1 reset:f2 reset:f3 reset:f4
        d8	gatepulse 1
	p0:sp0 	ph0
	d4d10u 	groff

if(PVM_ppgFlag1) ;long TE mode
{
        ;----------------------------------slice rephase--------------------
	d10 	grad{(0)|(0)|(t1)}
	d4d2 	groff
        ;----------------------------------read dephase + phase encoding----
        d11      grad{(t2)|r2d(t3)|r3d(t4)} 
	d4d10u   groff
}
if(!PVM_ppgFlag1) ;short TE mode
{
        ;----------------------slice rephase, read dephase, phase encoding----
	d10 	grad{(t2)|r2d(t3)|(t1)+r3d(t4)}
	d4d2 	groff
}

;----------------------------------frequency encoding---------------
        denab   grad{(t5)|(0)|(0)} 
	ADC_INIT_B_4ch(NOPH, ph2)  ;ph1 is used (see recph)
        aqq	ADC_START_4ch	
;----------------------------------read spoiler + phase encoding----
        d11     grad{(t6)|r2d(t7)|r3d(t8)} ; 2nd and 3rd dims
	d12     grad{(t6)| (0) | (0) }
	d3	groff
        ADC_END_4ch
        d0d40u
;----------------------------------slice and movie loop-----------------------	
subr FlowSatInc()
if(NI > 1)
{
	2.5u islice
	lo to slice times NI
}
        d20
;----------------------------------averaging loop-------------------
if(NA > 1)
{
	2.5u ipp1 ipp2
        2.5u recph1 ph1
        2.5u recph2 ph1
        2.5u recph3 ph1
        2.5u recph4 ph1
	lo to start times NA
        2.5u rpp1 rpp2
        2.5u recph1 ph1
        2.5u recph2 ph1
        2.5u recph3 ph1
        2.5u recph4 ph1
}
;----------------------------------2d loop--------------------------
	igrad r2d
	lo to start times l3
        "l3=l1"
;----------------------------------3d loop--------------------------
	igrad r3d
	lo to start times l2
        2.5u
        lo to start times NAE
        2.5u
#include <Evolution.mod>
        2.5u
        lo to start times NR
SETUP_GOTO(start)

exit

ph0 = 0 
ph1 = 0 2 1 3
ph2 = 0 2 3 1



	





